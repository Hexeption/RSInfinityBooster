buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath group: 'com.modrinth.minotaur', name: 'Minotaur', version: '2+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: "com.matthewprenger.cursegradle"
apply plugin: 'maven-publish'
apply plugin: 'com.modrinth.minotaur'

version = "1.20.1-1.0"
def env = System.getenv()
if (env.GITHUB_RUN_NUMBER) {
    version = version + "+" + "${env.GITHUB_RUN_NUMBER}"
}

group = 'uk.co.hexeption'
archivesBaseName = 'RSInfinityBooster'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)
println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))

minecraft {
    mappings channel: 'official', version: '1.20.1'

    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=rsinfinitybooster.mixins.json"

            mods {
                rsinfinitybooster {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=rsinfinitybooster.mixins.json"

            mods {
                rsinfinitybooster {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')
            // property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            args '--mod', 'rsinfinitybooster', '--all',
                    '--existing', file('src/main/resources').toString(),
                    '--existing', file('src/generated/resources').toString(),
                    '--output', file('src/generated/resources/')

            mods {
                rsinfinitybooster {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url = uri("https://maven.pkg.github.com/refinedmods/refinedstorage")
        /* As of december 2021, GitHub packages requires authentication.
           The password below is a personal access token that has read access to the Refined Mods repos.
           It can be reused in other projects.
           See: https://github.community/t/download-from-github-package-registry-without-authentication/14407/38 and
                https://github.community/t/download-from-github-package-registry-without-authentication/14407/44
         */
        credentials {
            username = "anything"
            password = "\u0067hp_oGjcDFCn8jeTzIj4Ke9pLoEVtpnZMP4VQgaX"
        }
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.0.3'
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    compileOnly "com.refinedmods:refinedstorage:1.12.4"
    runtimeOnly fg.deobf("com.refinedmods:refinedstorage:1.12.4") {
        transitive false
    }
    compileOnly fg.deobf("curse.maven:rebornstorage-256662:4861934")
    runtimeOnly fg.deobf("curse.maven:rebornstorage-256662:4861934")

    runtimeOnly fg.deobf("mezz.jei:jei-1.20.1-forge:15.0.0.12")
}


jar {
    manifest {
        attributes([
                "Specification-Title"     : "rsinfinitybooster",
                //"Specification-Vendor": "rsinfinitybooster authors",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                //"Implementation-Vendor": "rsinfinitybooster authors",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "rsinfinitybooster.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

mixin {
    add sourceSets.main, "rsinfinitybooster.refmap.json"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    archiveClassifier = 'deobf'
}

artifacts {
    archives jar
    archives sourcesJar
    archives deobfJar
}

curseforge {
    if (env.CURSE_API_KEY) {
        apiKey = env.CURSE_API_KEY
    }
    project {
        id = '446870'
        changelog = 'A changelog can be found at https://github.com/Hexeption/RSInfinityBooster'
        releaseType = 'release'
        addGameVersion '1.20.1'

        mainArtifact(jar) {
            displayName = "RSInfinityBooster v$version"
        }
        relations {
            requiredDependency 'refined-storage'
        }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact deobfJar
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
        if (env.REPO_PASSWORD) {
            maven {
                url = "https://nexus.hexeption.dev/repository/hex-mods/"
                credentials {
                    username = "admin"
                    password = env.REPO_PASSWORD
                }
            }
        }
    }
}

modrinth {
    if (env.MODRINTH_API_KEY) {
        token = env.MODRINTH_API_KEY
    }
    projectId = 'Sjzdj2cy'
    versionName = "RSInfinityBooster v$version"
    changelog = 'A changelog can be found at https://github.com/Hexeption/RSInfinityBooster'
    versionNumber = "$version"
    uploadFile = jar
    gameVersions = ["1.20.1"]
    loaders = ["forge"]
    dependencies {
        required.version "refined-storage", "1.12.3"
    }
}